plugins {
    id 'java'
    id "idea"
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id "net.minecraftforge.gradle.forge"
    id "org.spongepowered.mixin"
}

group = 'org.cubewhy.soar'
version = '1.0-SNAPSHOT'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

compileJava.options.encoding = "UTF-8"

configurations {
    include
    implementation.extendsFrom(include)

    external
    compile.extendsFrom(external)

    runtimeOnly.canBeResolved = true
}

repositories {
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
    mavenCentral()
}

dependencies {
    include fileTree(dir: "libs", includes: ['*.jar'])

    // Don't update mixin
    include("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
        exclude module: "slf4j-api"
    }

    annotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"

    include "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    include 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'

    implementation 'org.jetbrains:annotations:24.0.1'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
}

shadowJar {
    archiveClassifier.set("fatjar")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude "native-binaries/**"

    exclude "LICENSE.txt"

    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"

    exclude "org/junit/**"
}


minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += ["-Dfml.coreMods.load=me.eldodebug.soar.injection.transformer.TransformerLoader", "-Xmx2048m -Xms2048m", "-Ddev-mode"]
}

jar {
    manifestContentCharset 'utf-8'
    metadataCharset 'utf-8'
    manifest.attributes(
            "FMLCorePlugin": "me.eldodebug.soar.injection.transformer.TransformerLoader",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.soar.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
//            "FMLAT": "soar_at.cfg"
    )
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.soar.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    rename "(.+_at.cfg)", "META-INF/\$1"
}

tasks.register('moveResources') {
    copy {
        from "${buildDir}/resources/main"
        into "${buildDir}/classes/java"
    }
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)